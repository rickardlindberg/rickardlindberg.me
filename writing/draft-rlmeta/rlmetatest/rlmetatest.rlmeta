compiler RLMetaTest {
  Program = *TestCase:x {
    "set -e" . x
  };
  TestCase = '# ' toEol:x verbatim:y ':'\n *example:z {
    "echo 'compiling " @x "'" .
    "cat << 'EOF' | tee test-compiler.rlmeta | python ./rlmeta/rlmeta.py > test-compiler.py" .
    y
    "EOF" .
    "echo 'ok'" .
    z
  };
  example = verbatim:x '==>'\n verbatim:y '---'\n {
    "cat << 'EOF' > test-src" . x "EOF" .
    "cat << 'EOF' > test-out" . y "EOF" .
    "cat test-src | python test-compiler.py > test-actual-out" .
    "diff test-out test-actual-out" .
  };
  verbatim = *line;
  line     = '  ' toEol:x { @x }
           | \n:x         { @x }
           ;
  toEol    = *(!\n any):x { -> $"".join(x.value())$ };
}
