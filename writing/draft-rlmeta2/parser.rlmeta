Parser {
  grammar =
    | name:x space '{' rule*:ys space '}'      -> ["Grammar" x ~ys]
  rule =
    | name:x space '=' choices:y               -> ["Rule" x y]
  choices =
    | (space '|' | nothing)
      sequence:x (space '|' sequence)*:xs      -> ["Or" x ~xs]
  sequence =
    | expr:x expr*:xs                          -> ["Scope" ["And" x ~xs]]
  expr =
    | expr1:x space ':' name:y                 -> ["Bind" y x]
    | expr1
  expr1 =
    | expr2:x space '*'                        -> ["Star" x]
    | space '!' expr2:x                        -> ["Not" x]
    | expr2
  expr2 =
    | space '->' hostExpr:x                    -> ["SemanticAction" x]
    | space 'nothing' !nameChar                -> ["MatchNothing"]
    | name:x !(space '=')                      -> ["Apply" x]
    | string:x                                 -> ["MatchString" x]
    | charseq:x                                -> ["MatchCharsec" x]
    | space '.'                                -> ["MatchAny"]
    | space '(' choices:x space ')'            -> x
    | space '\'' char:x '\'-\'' char:y '\''    -> ["MatchCharRange" x y]
    | space '[' expr*:xs space ']'             -> ["MatchList" ["And" ~xs]]
  hostExpr =
    | string:x                                 -> ["String" x]
    | charseq:x                                -> ["String" x]
    | space '[' hostExprListItem*:xs space ']' -> ["List" ~xs]
    | space '{' stringBuild*:xs space '}'      -> ["ListBuilder" ~xs]
    | name:x space '(' hostExpr*:xs space ')'  -> ["FnCall" x ~xs]
    | name:x                                   -> ["VarLookup" x]
  hostExprListItem =
    | space '~' name:x                         -> ["ListItemSplice" x]
    | hostExpr
  stringBuild =
    | space '>'                                -> ["IndentBuilder"]
    | space '<'                                -> ["DedentBuilder"]
    | hostExpr
  string    = space '"' (!'"' char)*:xs '"'    -> join(xs)
  charseq   = space '\'' (!'\'' char)*:xs '\'' -> join(xs)
  char      = '\\' escape | .
  escape    = '\\' -> "\\" | '\'' -> "'"
            | '"'  -> "\"" | 'n'  -> "\n"
  name      = space nameStart:x nameChar*:xs   -> join([x ~xs])
  nameStart = 'a'-'z' | 'A'-'Z'
  nameChar  = 'a'-'z' | 'A'-'Z' | '0'-'9'
  space     = (' ' | '\t' | '\n')*
}
