Parser {
  file        = def*:xs !.                -> #List():defs
                                          -> #List():instructions
                                          -> #Dict():macros
                                          -> xs
                                          -> [["VM" defs instructions macros]]
  def         = instruction | blankline | macro | vm
  vm          = 'def ' name:x
                restline:y block:z        -> defs(["Def" x y ~z])
  instruction = 'definstruction ' name:x
                args:y ':\n' block:z      -> instructions(["Instruction" x ~y ~z])
  macro       = 'defmacro ' name:x
                ':\n' block:y             -> macros(x y)
  args        = '(' arg*:xs ')'           -> xs
  arg         = name:x (',' | -> "") ' '* -> ["Arg" x]
  block       = blockline*
  blockline   =
    | '    ' ' '*:xs '#' name:y '\n'      -> ["Macro" y { xs }]
    | '    ' restline:x                   -> ["Line" x]
  restline    = (!'\n'.)*:xs '\n'         -> { xs "\n" }
  name        = char:x char*:xs           -> { x xs }
  char        = 'a'-'z' | 'A'-'Z' | '_'
  blankline   = '\n'
}
