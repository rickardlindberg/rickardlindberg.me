CodeGenerator {
  Grammar       = .:x ast*:ys -> ["Grammar" x ~~ys]
  Rule          = .:x ast:y   -> [["Rule" x]
                                  ~y
                                  ["I" "RETURN"]]
  Or            =
    | ast:x Or:y              -> label():a -> label():b
                              -> [["I" "BACKTRACK"]
                                  ["Target" a]
                                  ~x
                                  ["I" "COMMIT"]
                                  ["Target" b]
                                  ["Label" a]
                                  ~y
                                  ["Label" b]]
    | ast
  Scope         = ast:x       -> [["I" "PUSH_SCOPE"]
                                  ~x
                                  ["I" "POP_SCOPE"]]
  And           = ast*:xs     -> [~~xs]
  Bind          = .:x ast:y   -> [~y
                                  ["I" "BIND"]
                                  ["Expr" x]]
  Star          = ast:x       -> label():a -> label():b
                              -> [["I" "LIST_START"]
                                  ["Label" a]
                                  ["I" "BACKTRACK"]
                                  ["Target" b]
                                  ~x
                                  ["I" "LIST_APPEND"]
                                  ["I" "COMMIT"]
                                  ["Target" a]
                                  ["Label" b]
                                  ["I" "LIST_END"]]
  Not           = ast:x       -> label():a -> label():b
                              -> [["I" "BACKTRACK"]
                                  ["Target" b]
                                  ~x
                                  ["I" "COMMIT"]
                                  ["Target" a]
                                  ["Label" a]
                                  ["I" "FAIL"]
                                  ["Expr" "no match expected"]
                                  ["Label" b]]
  MatchCallRule =             -> [["I" "MATCH_CALL_RULE"]]
  MatchRule     = .:x         -> [["I" "CALL"]
                                  ["Target" x]]
  MatchRange    = .:x .:y     -> [["I" "MATCH_RANGE"]
                                  ["Expr" x]
                                  ["Expr" y]]
  MatchAny      =             -> [["I" "MATCH_ANY"]]
  MatchList     = ast:x       -> [["I" "PUSH_STREAM"]
                                  ~x
                                  ["I" "POP_STREAM"]]
  MatchObject   = .:x         -> [["I" "MATCH_OBJECT"]
                                  ["Expr" x]]
  Action        = .*:xs       -> [["I" "ACTION"]
                                  ["Action" xs]]
  asts          = ast*:xs !.  -> xs
  ast           = [%:x]       -> x
}
